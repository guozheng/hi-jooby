buildscript {

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        /**
         * joobyRun
         */
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.6.2'
        classpath "io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE"
        classpath 'org.jooby:jooby-gradle-plugin:1.6.0'
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.1"
    }
}

/* * We are writing a standalone application. */
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "io.spring.dependency-management"
apply plugin: "com.google.osdetector"
apply plugin: 'application'
apply plugin: 'jooby'

/* * Some classic project configuration. */
version = 0.1
mainClassName = "hi.jooby.App"
sourceCompatibility = 1.8

/* * Dependencies will be downloaded from the Maven repository. */
repositories {
    mavenLocal()
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.jooby:jooby-bom:1.6.0"
    }
}

/* * A minimal project needs the framework and a webserver. */
dependencies {
    compile 'org.jooby:jooby:1.6.0'
    compile 'org.jooby:jooby-netty:1.6.0'
    compile "io.netty:netty-transport-native-epoll:${dependencyManagement.importedProperties['netty.version']}:${osdetector.classifier.contains('linux') ? 'linux-x86_64' : ''}"
    compile "io.netty:netty-tcnative-boringssl-static:${dependencyManagement.importedProperties['boringssl.version']}"

    compile 'ch.qos.logback:logback-classic:1.2.3'

    testCompile 'org.testng:testng:6.14.3'
    testCompile 'io.rest-assured:rest-assured:3.3.0'
}

/* * We diverge from the default resources structure to adopt the Jooby standard. */
sourceSets.main.resources {
    srcDirs = ['conf', 'public']
}

joobyRun {
    mainClassName = 'hi.jooby.App'
    compiler = 'on'
    includes = ['**/*.class', '**/*.conf', '**/*.properties']
    excludes = []
    logLevel = 'info'
    srcExtensions = [".java", ".kt", ".conf", ".properties"]
}